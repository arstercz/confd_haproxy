#!/usr/bin/env perl
=pod

=head1 NAME

checkmysqlslave -- check mysql whether is slave or not, set new slave to consul when current slave is abnormal.

=head1 SYNOPSIS

Usage: checkmysqlslave [OPTION...]

=head1 RISKS

checkmysqlslave will find all slaves by the master info in configure file.

=cut

# cz 20171012

use strict;
use warnings;
use Getopt::Long;
use Data::Dumper;
use Getopt::Long;
use Carp;
use JSON;
use LWP::UserAgent;
use HTTP::Request::Common;
use MIME::Base64;
use POSIX qw(strftime);
use Config::IniFiles;
use English '-no_match_vars';

my($conf,$debug,$tag,$token,$help);
my $consul  = "localhost:8500";
GetOptions(
  "conf=s"   => \$conf,
  "debug!"   => \$debug,
  "tag=s"    => \$tag,
  "consul=s" => \$consul,
  "token=s"  => \$token,
  "help!"    => \$help,
) or die "error:$!";

if ($help) {
  system("perldoc $0");
  exit(0);
}

unless ($consul) {
  echo_msg("error - need consul connection info");
  exit 1;
}

# get database file handle.
$conf = "./$conf" if $conf && $conf =~ /^[^\/]/;

sub get_host{
  my $cfg = Config::IniFiles->new( -file => "$conf" );
  return $cfg->val( "$tag", 'host');
}

sub get_port{
  my $cfg = Config::IniFiles->new( -file => "$conf" );
  return $cfg->val( "$tag", 'port');
}

sub get_user{
  my $cfg = Config::IniFiles->new( -file => "$conf" );
  return $cfg->val( "$tag", 'user');
}

sub get_pass{
  my $cfg = Config::IniFiles->new( -file => "$conf" );
  return $cfg->val( "$tag", 'password');
}

sub get_time {
  return 
    strftime("%Y-%m-%dT%H:%M:%S", localtime(time));
}

sub echo_msg {
  my $msg = shift;
  my $now = get_time();
  print "[$now] $msg\n";
}

# get current key value
sub get_curr_key {
    my ( $ua, $tag )   = @_;
    my $keyurl = "http://$consul/v1/kv/haproxy/mysqlslave/$tag"
               . do { defined($token)
                      ? "?token=$token"
                      : ""
                    }
               ;

    # get key value
    my $res = $ua->get("$keyurl");
    unless ($res->is_success) {
       return;
    }

    my $decode = $res->decoded_content;
    $decode    =~ s/(?:^\[|\]$)//g;
    my $text = decode_json($decode);

    my $value = defined($text->{'Value'})
              ? decode_base64($text->{'Value'})
              : undef ;
    return $value;
}

# set alive key value
sub set_alive_key {
  my ( $ua, $tag, $value) = @_;
  my $keyurl = "http://$consul/v1/kv/haproxy/mysqlslave/$tag"
             . do { defined($token)
                    ? "?token=$token"
                    : ""
                  }
             ;
  # post key value
  my $request  = 
    HTTP::Request::Common::PUT(
       $keyurl,
       'User-Agent' => 'arstercz_v0.1',
       'Content' => "$value"
    );
  my $res = $ua->request($request);
  #print Dumper($res);
  unless ($res->is_success) {
    return 0;
  }
  my $decode = $res->decoded_content;
  if ($decode eq 'true') {
    return "ok";
  }
  return;
}

sub get_dbhelper {
    my $host     = shift;
    my $port     = shift;
    my $user     = shift;
    my $password = shift;
    my $database = shift;
    my $charset  = shift;
    my $driver   = shift;

    printf("[%s] connect to %s, %d, %s, xxxxxxxx ...\n", 
           get_time(), $host, $port, $user);
    my $dbpre = Repl::dbh->new(
        host     => $host,
        port     => $port,
        user     => $user,
        password => $password,
        charset  => $charset,
        driver   => $driver,
    );
    my $dbh = $dbpre->get_dbh($database, { AutoCommit => 1 });
    return undef unless(defined $dbh);
    return Repl::DBHelper->new( dbh => $dbh );
}

sub check_slave {
  my($host, $port, $user, $password, $tag) = @_;
  unless($user && $password) {
    echo_msg("cann't get $tag list from conf file");
    exit 1;
  }
  my $dbhelper = get_dbhelper($host, $port, $user, $password, 
                 'information_schema', 'utf8', 'mysql');
  unless (defined($dbhelper)) {
    echo_msg("cann't connect to $host:$port with $tag");
    return 0; # cann't connect to mysql
  }
  my %status = $dbhelper->check_slave_status("$host:$port");
  if($status{Status} == 0) {
    return 1; # it's slave
  }
  return 0; # it's not slave
}

sub check_slave_ok {
  my $list = shift;
  my $tag  = shift;
  my $info = decode_json($list);
  # check mysql is slave which in redis list.
  unless(check_slave($info->{host}, $info->{port},
         get_user(), get_pass(), $tag)
    ) {
    echo_msg("$tag - check slave error");
    return 0;
  }
  return 1;
}

sub get_slave_msg {
  my $host_info = shift;
  my @slave;
  foreach my $k (%$host_info) {
    if (exists $host_info->{$k}->{'slave'}) {
      foreach my $key (@{$host_info->{$k}->{'slave'}}) {
        foreach my $kv (keys %$key) {
          push @slave, "$kv";
        }
      }
    }
  }
  return @slave;
}

sub set_new_slave {
  my $ua  = shift;
  my $tag = shift;
  my $xhost = get_host();
  my $xport = get_port();
  my $xuser = get_user();
  my $xpass = get_pass();
  # connect database that in db.conf
  unless($xuser && $xpass) {
    echo_msg("cann't get $tag list from conf file");
    exit 1;
  }
  my $dbhelper = get_dbhelper($xhost, $xport, $xuser, $xpass,
                      'information_schema', 'utf8', 'mysql');
  
  my %hosts_repl;
  $dbhelper->get_slave_by_master(
             $xhost, $xport, 'information_schema',
             $xuser, $xpass, \%hosts_repl, 0);

  my @slave = get_slave_msg(\%hosts_repl);
  echo_msg("get all slave by the master $xhost:$xport") if @slave;

  #set the first slave if there is slaves
  # 10.1.1.2:3306
  my $shost;
  my $sport;
  if (defined($slave[0])) {
    ($shost, $sport) = split(/:/, $slave[0]);
  }
  else {
    # there is no slave, set master.
    ($shost, $sport) = ($xhost, $xport);
  }
  my $proxyport = $sport + 10050; #default add 10050
  if(set_alive_key($ua, $tag,  
     "{\"name\":\"$tag\", \"host\":\"$shost\", \"port\":$sport, \"proxyport\":$proxyport}"))
  {
    echo_msg("set new $shost:$sport to consul $tag ok");
  }
  else {
    echo_msg("set new $shost:$sport to consul $tag error");
  }
}

#http client
my $ua = LWP::UserAgent->new;
$ua->timeout(3);

# loop detection
$| = 1;
while(1) {
  #check if exists in consul
  #consul key: /haproxy/mysqlslave/slave3306
  my $msg = get_curr_key($ua, $tag);
  if(defined($msg) && length($msg)) {
    if(check_slave_ok($msg, $tag)) {
      echo_msg("$tag in consul is ok");
    }
    else {
      set_new_slave($ua, $tag);
    }
  }
  else {
    echo_msg("there is no $tag in consul, set new...");
    set_new_slave($ua, $tag);
  }
  sleep 1;
}

# ###################################################################
# Documentation.
# ###################################################################

=head1 OPTIONS

=over 4

=item conf

database config file, depend on DBD::mysql mysql_read_default_group
method, eg:

    [mysql3308]
    database=information_schema
    host=10.0.1.1
    port=3308
    user=dbuser
    password=xxxxxxxx
    default-character-set=utf8

=item tag

instance name from config file. such as mysql3308 with conf option.

=item consul

consul http address, default is "localhost:8500"

=item token

consul acl_token, default is undef.

=item help

print help info.

=back

=head1 AUTHOR

zhe.chen <chenzhe07@gmail.com>

=head1 CHANGELOG

v0.1.0 version

=cut

package Repl::dbh;

# Get the database handle which user use, and this database
# handle object should be destroy when leave MySQL database.
use strict;
use warnings FATAL => 'all';
use constant PTDEBUG => $ENV{PTDEBUG} || 0;
use English qw(-no_match_vars);
use DBI;
use vars qw($VERSION @ISA @EXPORT @EXPORT_OK);

use Data::Dumper;
$Data::Dumper::Indent    = 1;
$Data::Dumper::Sortkeys  = 1;
$Data::Dumper::Quotekeys = 0;

require Exporter;
@ISA     = qw(Exporter);
@EXPORT  = qw( get_dbh disconnect );
$VERSION = '0.1.0';

eval { require DBI; };

if ($@) {
    die "Cannot connect to MySQL because the Perl DBI module is not "
      . "installed or not found.  Run 'perl -MDBI' to see the directories "
      . "that Perl searches for DBI.  If DBI is not installed, try:\n"
      . "  Debian/Ubuntu  apt-get install libdbi-perl\n"
      . "  RHEL/CentOS    yum install perl-DBI\n"
      . "  OpenSolaris    pkg install pkg:/SUNWpmdbi\n";
}

sub host {
    my $self = shift;
    $self->{host} = shift if @_;
    return $self->{host};
}

sub port {
    my $self = shift;
    $self->{port} = shift if @_;
    return $self->{port};
}

sub user {
    my $self = shift;
    $self->{user} = shift if @_;
    return $self->{user};
}

sub password {
    my $self = shift;
    $self->{password} = shift if @_;
    return $self->{password};
}

sub charset {
    my $self = shift;
    $self->{charset} = shift if @_;
    return $self->{charset};
}

sub driver {
    my $self = shift;
    $self->{driver} = shift if @_;
    return $self->{driver};
}

sub new {
    my ( $class, %args ) = @_;
    my @required_args = qw(host port user password);
    PTDEBUG && print Dumper(%args);

    foreach my $arg (@required_args) {
        warn "I need a $arg argument" unless $args{$arg};
    }

    my $self = {};
    bless $self, $class;

    # options should be used.
    $self->host( $args{'host'}         || 127.0.0.1 );
    $self->port( $args{'port'}         || 3306 );
    $self->user( $args{'user'}         || 'audit' );
    $self->password( $args{'password'} || '' );
    $self->charset( $args{'charset'}   || 'utf8' );
    $self->driver( $args{'driver'}     || 'mysql' );

    return $self;
}

sub get_dbh {
    my ( $self, $database, $opts ) = @_;
    $opts ||= {};
    my $host     = $self->{host};
    my $port     = $self->{port};
    my $user     = $self->{user};
    my $password = $self->{password};
    my $charset  = $self->{charset};
    my $driver   = $self->{driver};

    my $defaults = {
        AutoCommit         => 0,
        RaiseError         => 1,
        PrintError         => 0,
        ShowErrorStatement => 1,
        mysql_enable_utf8  => ( $charset =~ m/utf8/i ? 1 : 0 ),
    };
    @{$defaults}{ keys %$opts } = values %$opts;

    #if ( $opts->{mysql_use_result} ) {
    #    $defaults->{mysql_use_result} = 1;
    #}

    my $dbh;
    my $tries = 2;
    while ( !$dbh && $tries-- ) {
        PTDEBUG
          && print Dumper(
            join( ', ', map { "$_=>$defaults->{$_}" } keys %$defaults ) );
        $dbh = eval {
            DBI->connect(
                "DBI:$driver:mysql_connect_timeout=1:database=$database;host=$host;port=$port",
                $user, $password, $defaults );
        };

        if ( !$dbh && $@ ) {
            if ( $@ =~ m/locate DBD\/mysql/i ) {
                warn
"Cannot connect to MySQL because the Perl DBD::mysql module is "
                  . "not installed or not found.  Run 'perl -MDBD::mysql' to see "
                  . "the directories that Perl searches for DBD::mysql.  If "
                  . "DBD::mysql is not installed, try:\n"
                  . "  Debian/Ubuntu  apt-get install libdbd-mysql-perl\n"
                  . "  RHEL/CentOS    yum install perl-DBD-MySQL\n"
                  . "  OpenSolaris    pgk install pkg:/SUNWapu13dbd-mysql\n";
            }
            elsif ( $@ =~ m/not a compiled character set|character set utf8/i )
            {
                PTDEBUG && print 'Going to try again without utf8 support\n';
                delete $defaults->{mysql_enable_utf8};
            }
            if ( !$tries ) {
                warn "$@";
                return;
            }

        }
    }

    if ( $driver =~ m/mysql/i ) {
        my $sql;
        $sql = 'SELECT @@SQL_MODE';
        PTDEBUG && print "+-- $sql\n";

        my ($sql_mode) = eval { $dbh->selectrow_array($sql) };
        warn "Error getting the current SQL_MORE: $@" if $@;

        if ($charset) {
            $sql = qq{/*!40101 SET NAMES "$charset"*/};
            PTDEBUG && print "+-- $sql\n";
            eval { $dbh->do($sql) };
            warn "Error setting NAMES to $charset: $@" if $@;
            PTDEBUG && print "Enabling charset to STDOUT\n";
            if ( $charset eq 'utf8' ) {
                binmode( STDOUT, ':utf8' )
                  or warn "Can't binmode(STDOUT, ':utf8'): $!\n";
            }
            else {
                binmode(STDOUT) or warn "Can't binmode(STDOUT): $!\n";
            }
        }

        $sql =
            'SET @@SQL_QUOTE_SHOW_CREATE = 1'
          . '/*!40101, @@SQL_MODE=\'NO_AUTO_VALUE_ON_ZERO'
          . ( $sql_mode ? ",$sql_mode" : '' ) . '\'*/';
        PTDEBUG && print "+-- $sql\n";
        eval { $dbh->do($sql) };
        warn "Error setting SQL_QUOTE_SHOW_CREATE, SQL_MODE"
          . ( $sql_mode ? " and $sql_mode" : '' ) . ": $@"
          if $@;
    }

    if (PTDEBUG) {
        print Dumper(
            $dbh->selectrow_hashref(
'SELECT DATABASE(), CONNECTION_ID(), VERSION()/*!50038, @@hostname*/'
            )
        );
        print "+-- 'Connection info:', $dbh->{mysql_hostinfo}\n";
        print Dumper(
            $dbh->selectall_arrayref(
                "SHOW VARIABLES LIKE 'character_set%'",
                { Slice => {} }
            )
        );
        print '+-- $DBD::mysql::VERSION:' . "$DBD::mysql::VERSION\n";
        print '+-- $DBI::VERSION:' . "$DBI::VERSION\n";
    }
    return $dbh;
}

# handle should be destroy.
sub disconnect {
    my ( $self, $dbh ) = @_;
    PTDEBUG && $self->print_active_handles( $self->get_dbh );
    $dbh->disconnect;
}

sub print_active_handles {
    my ( $self, $thing, $level ) = @_;
    $level ||= 0;
    printf(
        "# Active %sh: %s %s %s\n",
        ( $thing->{Type} || 'undef' ),
        "\t" x $level,
        $thing,
        ( ( $thing->{Type} || '' ) eq 'st' ? $thing->{Statement} || '' : '' )
    ) or warn "Cannot print: $OS_ERROR";
    foreach my $handle ( grep { defined } @{ $thing->{ChildHandles} } ) {
        $self->print_active_handles( $handle, $level + 1 );
    }
}

1;

package Repl::DBHelper;
use strict;
use warnings FATAL => 'all';
use English qw(-no_match_vars);
use Carp qw(croak);
use Data::Dumper;

use constant Status => "Status";
use constant Errstr => "Errstr";

#show master status output
use constant File              => "File";
use constant Position          => "Position";
use constant Binlog_Do_DB      => "Binlog_Do_DB";
use constant Binlog_Ignore_DB  => "Binlog_Ignore_DB";
use constant Executed_Gtid_Set => "Executed_Gtid_Set";

#show slave status output
use constant Slave_IO_State              => "Slave_IO_State";
use constant Slave_SQL_Running           => "Slave_SQL_Running";
use constant Slave_IO_Running            => "Slave_IO_Running";
use constant Master_Log_File             => "Master_Log_File";
use constant Master_Host                 => "Master_Host";
use constant Master_User                 => "Master_User";
use constant Master_Port                 => "Master_Port";
use constant Replicate_Do_DB             => "Replicate_Do_DB";
use constant Replicate_Ignore_DB         => "Replicate_Ignore_DB";
use constant Replicate_Do_Table          => "Replicate_Do_Table";
use constant Replicate_Ignore_Table      => "Replicate_Ignore_Table";
use constant Replicate_Wild_Do_Table     => "Replicate_Wild_Do_Table";
use constant Replicate_Wild_Ignore_Table => "Replicate_Wild_Ignore_Table";
use constant Read_Master_Log_Pos         => "Read_Master_Log_Pos";
use constant Relay_Master_Log_File       => "Relay_Master_Log_File";
use constant Exec_Master_Log_Pos         => "Exec_Master_Log_Pos";
use constant Relay_Log_File              => "Relay_Log_File";
use constant Relay_Log_Pos               => "Relay_Log_Pos";
use constant Seconds_Behind_Master       => "Seconds_Behind_Master";
use constant Last_Errno                  => "Last_Errno";
use constant Last_Error                  => "Last_Error";
use constant Retrieved_Gtid_Set          => "Retrieved_Gtid_Set";
use constant Auto_Position               => "Auto_Position";

#general sql list
use constant Show_One_Variable_SQL  => "SHOW GLOBAL VARIABLES LIKE ?";
use constant Show_Slave_Status_SQL  => "SHOW SLAVE STATUS";
use constant Show_Processlist_SQL   => "SHOW PROCESSLIST";
use constant Show_Master_Status_SQL => "SHOW MASTER STATUS";
use constant Get_Num_Workers_SQL =>
  "SELECT \@\@global.slave_parallel_workers AS Value";
use constant Get_MaxAllowedPacket_SQL =>
  "SELECT \@\@global.max_allowed_packet AS Value";
use constant Is_Readonly_SQL  => "SELECT \@\@global.read_only As Value";
use constant Has_Gtid_SQL     => "SELECT \@\@global.gtid_mode As Value";
use constant Get_ServerID_SQL => "SELECT \@\@global.server_id As Value";
use constant Get_Version_SQL  => "SELECT VERSION() AS Value";
use constant Get_Database_SQL => "SELECT GROUP_CONCAT(SCHEMA_NAME) AS DBS FROM information_schema.SCHEMATA
WHERE SCHEMA_NAME NOT IN('mysql','test','information_schema','performance_schema')";

sub new {
    my ( $class, %args ) = @_;
    my @required_args = qw(dbh);
    foreach my $arg (@required_args) {
        die "I need a $arg argument" unless $args{$arg};
    }
    my $self = {
        dbh           => undef,
        connection_id => undef,
        has_gtid      => undef,
        is_mariadb    => undef,
    };
    bless $self, $class;
    $self->{dbh} = $args{'dbh'};
    return $self;
}

sub get_variable {
    my $self  = shift;
    my $query = shift;
    my $sth   = $self->{dbh}->prepare($query);
    $sth->execute();
    my $href = $sth->fetchrow_hashref;
    return $href->{Value};
}

# display one value that are not supported by select @@..
sub show_variable($$) {
    my $self = shift;
    my $cond = shift;
    my $sth  = $self->{dbh}->prepare(Show_One_Variable_SQL);
    $sth->execute($cond);
    my $href = $sth->fetchrow_hashref;
    return $href->{Value};
}

sub is_binlog_enabled($) {
    my $self  = shift;
    my $value = $self->show_variable("log_bin");
    return 1 if ( defined($value) && $value eq "ON" );
    return 0;
}

sub is_read_only($) {
    my $self = shift;
    return $self->get_variable(Is_Readonly_SQL);
}

sub has_gtid($) {
    my $self  = shift;
    my $value = $self->get_variable(Has_Gtid_SQL);
    if ( defined($value) && $value eq "ON" ) {
        $self->{has_gtid} = 1;
        return 1;
    }
    return 0;
}

sub get_num_workers($) {
    my $self = shift;
    return $self->get_variable(Get_Num_Workers_SQL);
}

sub get_version($) {
    my $self  = shift;
    my $value = return $self->get_variable(Get_Version_SQL);
    if ( $value =~ /MariaDB/ ) {
        $self->{is_mariadb} = 1;
    }
    return $value;
}

sub get_server_id($) {
    my $self = shift;
    return $self->get_variable(Get_ServerID_SQL);
}

sub get_max_allowed_packet($) {
    my $self = shift;
    return $self->get_variable(Get_MaxAllowedPacket_SQL);
}

sub get_database_list {
    my $self = shift;
    my $str  = "";
    my ($query, $sth, $href);
    $query = Get_Database_SQL;
    $sth   = $self->{dbh}->prepare($query);
    my $ret = $sth->execute();
    return if ( !defined($ret) || $ret != 1 );
    $href = $sth->fetchrow_hashref;
    if( defined $href->{DBS} && length($href->{DBS}) > 0 ) {
       $str .= sprintf("%s;", $href->{DBS});
    }
    return $str;
}

sub get_master_filter($) {
    my $self = shift;
    my ( $query, $sth, $href );
    my %values;
    my $str = "";
    $query = Show_Master_Status_SQL;
    $sth   = $self->{dbh}->prepare($query);
    my $ret = $sth->execute();
    return if ( !defined($ret) || $ret != 1 );

    $href = $sth->fetchrow_hashref;
    for my $filter_key ( Binlog_Do_DB, Binlog_Ignore_DB ) {
        if ( length( $href->{$filter_key} ) > 0 ) {
            $str .= sprintf( "%s: %s; ",
                lc($filter_key), uniq_and_sort( $href->{$filter_key} ) );
        }
    }

    return $str;
}

sub check_ping($) {
   my $self = shift;
   return $self->{dbh}->ping;
}

sub uniq_and_sort {
    my $str = shift;
    my @array = split( /,/, $str );
    my %count;
    @array = grep( !$count{$_}++, @array );
    @array = sort @array;
    return join( ',', @array );
}

sub check_slave_status {
    my $self        = shift;
    my $allow_dummy = shift;
    my ( $query, $sth, $href );
    my %status = ();

    unless ( $self->{dbh} ) {
        $status{Status} = 1;
        $status{Errstr} = "Database Handle is not defined!";
        return %status;
    }

    $query = Show_Slave_Status_SQL;
    $sth   = $self->{dbh}->prepare($query);
    my $ret = $sth->execute();
    if ( !defined($ret) || $ret != 1 ) {

        # I am not a slave
        $status{Status} = 1;

        # unexpected error
        if ( defined( $sth->errstr ) ) {
            $status{Status} = 2;
            $status{Errstr} =
                "Got error when executing "
              . Show_Slave_Status_SQL . ". "
              . $sth->errstr;
        }
        return %status;
    }

    $status{Status} = 0;
    $href = $sth->fetchrow_hashref;

    for my $key (
        Slave_IO_State,        Master_Host,
        Master_Port,           Master_User,
        Slave_IO_Running,      Slave_SQL_Running,
        Master_Log_File,       Read_Master_Log_Pos,
        Relay_Master_Log_File, Last_Errno,
        Last_Error,            Exec_Master_Log_Pos,
        Relay_Log_File,        Relay_Log_Pos,
        Seconds_Behind_Master, Retrieved_Gtid_Set,
        Executed_Gtid_Set,     Auto_Position
      )
    {
        $status{$key} = $href->{$key};
    }

    if (   !$status{Master_Host}
        || !$status{Master_Log_File} )
    {
        unless ($allow_dummy) {

            # I am not a slave
            $status{Status} = 1;
            return %status;
        }
    }

    my $str = "";
    for
      my $filter_key ( Replicate_Do_DB, Replicate_Ignore_DB, Replicate_Do_Table,
        Replicate_Ignore_Table, Replicate_Wild_Do_Table,
        Replicate_Wild_Ignore_Table )
    {
        $status{$filter_key} = uniq_and_sort( $href->{$filter_key} );
        if ( length( $href->{$filter_key} ) > 0 ) {
            $str .= sprintf( "%s: %s; ",
                lc($filter_key), uniq_and_sort( $href->{$filter_key} ) );
        }
    }
    $status{Filter} = $str;

    return %status;
}

sub get_threads_util {
    my $dbh = shift;
    my @threads;

    my $sth = $dbh->prepare(Show_Processlist_SQL);
    $sth->execute();

    while ( my $ref = $sth->fetchrow_hashref() ) {
        my $id         = $ref->{Id};
        my $user       = $ref->{User};
        my $host       = $ref->{Host};
        my $command    = $ref->{Command};
        my $state      = $ref->{State};
        my $query_time = $ref->{Time};
        my $info       = $ref->{Info};
        $info =~ s/^\s*(.*?)\s*$/$1/ if defined($info);

        push( @threads, $ref )
          if ( defined($command) && $command =~ /^Binlog Dump/ );
    }
    return @threads;
}

sub cut_host($) {
    my $info_ref = shift;
    my @hosts    = ();
    for (@$info_ref) {
        my ( $host, $sport ) = split( /:/, $_->{Host}, 2 );
        $host = '127.0.0.1' if $host =~/localhost/;
        push @hosts, $host;
    }

    return @hosts;
}

sub get_common_info($) {
    my $self = shift;

    my %common_hash = ();
    $common_hash{server_id} = $self->get_server_id();
    $common_hash{version}   = $self->get_version();
    $common_hash{read_only} = $self->is_read_only();
    $common_hash{databases} = $self->get_database_list(); 
    $common_hash{has_gtid}  = do {
        if ( $common_hash{version} =~ m/5.6/ ) {
            $self->has_gtid();
        }
        else {
            "Not Support";
        }
    };
    $common_hash{binlog_format} = $self->show_variable("binlog_format");
    $common_hash{binlog_enable} = $self->is_binlog_enabled();
    $common_hash{max_packet} =
      $self->get_max_allowed_packet() / 1024 / 1024 . 'MB';

    return \%common_hash;

}

sub get_slave_by_master($$$$$$$) {
    my $self       = shift;
    my $host       = shift;
    my $port       = shift;
    my $database   = shift;
    my $user       = shift;
    my $password   = shift;
    my $hosts_hash = shift;
    my $recurse    = shift;
    my %status     = ();

    my $dbpre = Repl::dbh->new(
        host     => $host,
        port     => $port,
        user     => $user,
        password => $password,
    );
    my $dbh = $dbpre->get_dbh( $database, { AutoCommit => 1 } );

    $self->{dbh} = $dbh;
    my @up_threads = Repl::DBHelper::get_threads_util($dbh);
    %status = $self->check_slave_status();
    if ( $status{Status} ) {
        $self->{dbh}                           = $dbh;
        $hosts_hash->{"$host:$port"}           = $self->get_common_info();
        $hosts_hash->{"$host:$port"}->{filter} = $self->get_master_filter();
    }

    my @slave_hosts = cut_host( \@up_threads );
    for my $slave (@slave_hosts) {
        if ($hosts_hash->{"$slave:$port"}) {
            next;
        }
        my $dbpre = Repl::dbh->new(
            host     => $slave,
            port     => $port,
            user     => $user,
            password => $password,
        );
        my $dbh_s = $dbpre->get_dbh( $database, { AutoCommit => 1 } );
        my $slave_hash;

        unless ( defined $dbh_s ) {
            $slave_hash->{"$slave:$port"}{status} = "connect error";
            push @{ $hosts_hash->{"$host:$port"}->{slave} }, $slave_hash;
            return;
        }
        $self->{dbh} = $dbh_s;
        my %status_slave = $self->check_slave_status();
        my @down_threads = Repl::DBHelper::get_threads_util($dbh_s);
        return unless defined $status_slave{Slave_SQL_Running};

        # slave has no slave.
        $slave_hash->{"$slave:$port"}               = $self->get_common_info();
        $slave_hash->{"$slave:$port"}->{filter}     = $status_slave{Filter};
        $slave_hash->{"$slave:$port"}->{repl_check} = do {
            if (   $status_slave{Slave_SQL_Running} eq 'Yes'
                && $status_slave{Slave_IO_Running} eq 'Yes' )
            {
                if ( $status_slave{Seconds_Behind_Master} < 30 ) {
                    "OK";
                }
                else {
                    "Delay @{ [$status_slave{Seconds_Behind_Master}] } Seconds";
                }
            }
            else {
                "Error";
            }
        };
        push( @{ $hosts_hash->{"$host:$port"}->{slave} }, $slave_hash );
        $self->{dbh}->disconnect();
        if ( @down_threads + 0 == 0 ) {
            next;
        }
        else {
            $self->get_slave_by_master(
                $slave, $port,
                $database,
                $user,
                $password,
                find_key_from_array(
                    $hosts_hash->{"$host:$port"},
                    "$slave:$port"
                ),
                $recurse
            ) if $recurse;
        }

    }
}

sub find_key_from_array {
    my $array_ref = shift;
    my $key       = shift;
    my $i         = 0;
    foreach ( @{ $array_ref->{slave} } ) {
        my ( $k, $v ) = each %$_;
        return $array_ref unless defined $k;
        if ( $k eq $key ) {
            return $array_ref->{slave}->[$i];
        }
        $i++;
    }
    return $array_ref;
}

1;
